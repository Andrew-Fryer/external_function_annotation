% Andrew Fryer, 2023

define program
    [Decl*]
end define

compounds
    '::
    '->
end compounds

tokens
    charlit "" % undefines character literals because rust uses a single quote for lifetimes
end tokens

define Decl
    'DefId '( [IslandGrammar] ') ': % todo: don't allow 'Call in here...
    'Thir '{
        [IslandGrammar]
    '}
end define

define IslandGrammar
    [Element*]
end define

define Element
      [Land]
    | [Water]
end define

define Land
    [Call]
end define

define Call
    'Call '{ 'ty ': [not_brace*] '{ [not_brace*] '} [IslandGrammar] '}
end define

define Water
      '{ [IslandGrammar] '}
    | '( [IslandGrammar] ')
    | '[ [IslandGrammar] ']
    | [Wave]
end define

define Wave
    % Note: if I make this a [token*], TXL gets very slow and gives TXL0127E
    [not_call_or_bracket_or_brace_or_parenthesis+] %| [id]
end define

define wildcard
    [token]
end define

define bracket
      '[
    | ']
end define

define brace
      '{
    | '}
end define

define parenthesis
      '(
    | ')
end define

define not_brace
    [not brace] [wildcard]
end define

define not_parenthesis
    [not parenthesis] [wildcard]
end define

define not_brace_or_parenthesis
    [not brace] [not_parenthesis]
end define

define not_bracket_or_brace_or_parenthesis
    [not bracket] [not_brace_or_parenthesis]
end define

define not_call_or_bracket_or_brace_or_parenthesis
    [not 'Call] [not_bracket_or_brace_or_parenthesis]
end define
