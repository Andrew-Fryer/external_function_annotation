% Andrew Fryer, 2023

define program
    [IslandGrammar]
end define

keys
    '{
    '}
    '(
    ')
    '[
    ']
    'Call
    % 'Func
end keys

tokens
    charlit "" % undefines character literals because rust uses a single quote for lifetimes
end tokens

define IslandGrammar
    [Element*]
end define

define Element
      [Land]
    | [Water]
end define

define Land
    'Call '{ [IslandGrammar] '}
end define

define Water
      '{ [IslandGrammar] '}
    | '( [IslandGrammar] ')
    | '[ [IslandGrammar] ']
    | [Wave]
end define

define Wave
    % Note: if I make this a [token*], TXL gets very slow and gives TXL0127E
    [token+] %| [id]
end define

rule remove_waves
    replace [Element*]
        w [Wave]
        remaining [Element*]
    by
        remaining
end rule

function main
    replace [program]
        p [program]
    construct all_calls [Land*]
        _ [^ p]
    by
        p [reparse all_calls]
end function
